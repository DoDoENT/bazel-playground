load("@rules_xcodeproj//xcodeproj:top_level_target.bzl", "top_level_target")
load("@rules_xcodeproj//xcodeproj:xcodeproj.bzl", "xcodeproj")
load("@rules_xcodeproj//xcodeproj:xcschemes.bzl", "xcschemes")

load(":ios-targets.bzl", "IOS_APP_TARGETS", "IOS_TEST_TARGETS")

xcodeproj(
    name = "ios-projects",
    project_name = "iOSProjects",
    tags = ["manual"],
    top_level_targets = [
        top_level_target(name, target_environments = ["device", "simulator"]) for name in IOS_APP_TARGETS + IOS_TEST_TARGETS
    ],
    xcschemes = [
        xcschemes.scheme(
            name = target.split(":")[-1].replace("-ios", ""),
            test = xcschemes.test(
                test_targets = [
                    xcschemes.test_target(target)
                ],
                build_targets = [
                    xcschemes.top_level_build_target("//test-support/ios-test/GoogleTestHost:GoogleTestHost"),
                ]
            ),
            run = xcschemes.run(
                launch_target = xcschemes.launch_target("//test-support/ios-test/GoogleTestHost:GoogleTestHost"),
            )
        ) for target in IOS_TEST_TARGETS
    ] + [
        xcschemes.scheme(
            name = target.split(":")[-1].replace("-ios", ""),
            run = xcschemes.run(
                launch_target = xcschemes.launch_target(target),
            )
        ) for target in IOS_APP_TARGETS
    ],
    xcode_configurations = {
        "Debug": {
            "//command_line_option:compilation_mode": "dbg",
            "//command_line_option:features": [],
        },
        "DevRelease": {
            "//command_line_option:compilation_mode": "fastbuild",
            "//command_line_option:features": [],
        },
        "Release": {
            "//command_line_option:compilation_mode": "opt",
            "//command_line_option:features": ["thin_lto"],
        }
    },
    target_compatible_with = [
        # note: this target needs to run on macOS as Xcode project is generated there
        "@platforms//os:macos",
    ],
)

##### Xcode versions ######

# based on instructions from here: https://www.smileykeith.com/2021/03/08/locking-xcode-in-bazel/
# also note: https://blog.bazel.build/2020/02/26/xcode-selection.html
# additionally: https://bazel.build/docs/bazel-and-apple#xcode-selection

xcode_version(
    name = "version26_2_0_17C52",
    version = "26.2.0.17C52",
    aliases = ["26.2.0.17C52", "26.2", "26.2.0", "17C52"],
    default_ios_sdk_version = "26.2",
    default_macos_sdk_version = "26.2",
)

available_xcodes(
    name = "available_xcodes",
    versions = [
        ":version26_2_0_17C52",
    ],
    default = ":version26_2_0_17C52",
)

xcode_config(
    name = "host",
    local_versions = ":available_xcodes",
    remote_versions = ":available_xcodes",
)
