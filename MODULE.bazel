module(name = "playground")

# Bazel rules begin

# Note: This needs to be before rules_cc in order to correctly configure the C++ toolchain.
# See here why: https://www.smileykeith.com/2025/09/21/understanding-apple-debug-info/
bazel_dep(name = "apple_support", version = "2.1.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "aspect_bazel_lib", version = "2.22.0")
bazel_dep(name = "bazel_skylib", version = "1.9.0")
bazel_dep(name = "bazelrc-preset.bzl", version = "1.8.0")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_build_error", version = "0.9.0")
bazel_dep(name = "rules_license", version = "1.0.0")

# Bazel rules end

# Host platform detection
host_platform = use_extension("//platforms:extension.bzl", "host_platform")
use_repo(host_platform, "host_platform")

# Apple rules begin

bazel_dep(name = "rules_apple", version = "4.3.3")
bazel_dep(name = "rules_swift", version = "3.4.1")
bazel_dep(name = "rules_xcodeproj", version = "3.5.1")

# Apple rules end

# LLVM rules begin

# TODO: Much more convenient than toolchains_llvm, but:
#  - no support for sanitizers yet (on roadmap; setting --@toolchains_llvm_bootstrapped//config:ubsan=True, does not help)
#  - no support for choosing LLVM version yet
# bazel_dep(name = "toolchains_llvm_bootstrapped", version = "0.4.1")
#
# llvm_toolchains = use_extension("@toolchains_llvm_bootstrapped//toolchain/extension:llvm.bzl", "llvm")
#
# llvm_toolchains.exec(arch = "x86_64", os = "linux")
# llvm_toolchains.exec(arch = "aarch64", os = "linux")
# llvm_toolchains.exec(arch = "aarch64", os = "darwin")
# llvm_toolchains.target(arch = "x86_64", os = "linux")
# llvm_toolchains.target(arch = "aarch64", os = "linux")
#
# use_repo(llvm_toolchains, "llvm_toolchains")
# register_toolchains("@toolchains_llvm_bootstrapped//toolchain:all")

bazel_dep(name = "toolchains_llvm", version = "1.6.0")

bazel_dep(name = "sysroot-noble-aarch64")
archive_override(
    module_name = "sysroot-noble-aarch64",
    integrity = "sha256-+mJ5891mJMVnMHDX+dixdD0Z8uEId5SBau2XjwFvFJU=",
    urls = [
        "https://artifactory.microblink.com:443/artifactory/bazel-prebuilt-packages/sysroots/sysroot-noble-aarch64.tar.xz",
    ],
)

bazel_dep(name = "sysroot-noble-x86_64")
archive_override(
    module_name = "sysroot-noble-x86_64",
    integrity = "sha256-25OeIr2YGIKxXuYvjcxZzQa7gE1qapKC3OZz6/9Fjhw=",
    urls = [
        "https://artifactory.microblink.com:443/artifactory/bazel-prebuilt-packages/sysroots/sysroot-noble-x86_64.tar.xz",
    ],
)

# Configure and register the toolchain.
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")

LLVM_VERSION = "21.1.8"

llvm.toolchain(
    name = "llvm_linux_intel",
    llvm_version = LLVM_VERSION,
    exec_os = "linux",
    exec_arch = "amd64",
    extra_llvm_distributions = {
        "LLVM-21.1.8-Linux-X64.tar.xz": "b3b7f2801d15d50736acea3c73982994d025b01c2f035b91ae3b49d1b575732b",
    }
)
llvm.sysroot(
    name = "llvm_linux_intel",
    label = "@sysroot-noble-x86_64//sysroot:directory",
    targets = ["linux-x86_64"]
)

use_repo(llvm, "llvm_linux_intel", "llvm_linux_intel_llvm")

llvm.toolchain(
    name = "llvm_linux_intel_haswell",
    llvm_version = LLVM_VERSION,
    extra_compile_flags = {
        "": [
            "-march=haswell",
            "-mtune=haswell",
            "-mavx",
            "-mavx2",
        ],
    },
)
# Reuse the toolchain root from llvm_linux_intel to avoid downloading LLVM twice
llvm.toolchain_root(
    name = "llvm_linux_intel_haswell",
    label = "@llvm_linux_intel_llvm//:BUILD",
)
llvm.extra_target_compatible_with(
    name = "llvm_linux_intel_haswell",
    constraints = [
        "//platforms/linux/intel_flavor:haswell",
    ],
)
llvm.sysroot(
    name = "llvm_linux_intel_haswell",
    label = "@sysroot-noble-x86_64//sysroot:directory",
    targets = ["linux-x86_64"]
)
use_repo(llvm, "llvm_linux_intel_haswell")

llvm.toolchain(
    name = "llvm_linux_arm",
    llvm_version = LLVM_VERSION,
    exec_os = "linux",
    exec_arch = "aarch64",
    extra_llvm_distributions = {
        "LLVM-21.1.8-Linux-ARM64.tar.xz": "65ce0b329514e5643407db2d02a5bd34bf33d159055dafa82825c8385bd01993",
    }
)

llvm.sysroot(
    name = "llvm_linux_arm",
    label = "@sysroot-noble-aarch64//sysroot:directory",
    targets = ["linux-aarch64"]
)

use_repo(llvm, "llvm_linux_arm")

# Haswell needs to be registered first, as it is more specific than the generic intel toolchain
register_toolchains(
    "@llvm_linux_intel_haswell//:all",
    "@llvm_linux_intel//:all",
    "@llvm_linux_arm//:all",
)
# LLVM rules end

register_execution_platforms(
    "//platforms/linux:x86_64",
    "//platforms/linux:arm64",
    "//platforms/macos:arm64",
    "//platforms/macos:x86_64",
)

# Python rules begin

bazel_dep(name = "rules_python", version = "1.7.0")

PYTHON_VERSION = "3.14"

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.defaults(python_version = PYTHON_VERSION)
python.toolchain(python_version = PYTHON_VERSION)

# https://rules-python.readthedocs.io/en/latest/api/rules_python/python/uv/uv.html
uv = use_extension("@rules_python//python/uv:uv.bzl", "uv", dev_dependency = True)
uv.configure()

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "coverage_pip_deps",
    python_version = PYTHON_VERSION,
    requirements_lock = "//macros/coverage:requirements_lock.txt",
)
use_repo(pip, "coverage_pip_deps")

# Python rules end

# Android rules begin
bazel_dep(name = "rules_android_ndk", version = "0.1.3")

# Patch to allow downloading NDK and additionally define some custom build flags
single_version_override(
    module_name = "rules_android_ndk",
    patch_strip = 1,
    patches = [
        "//3rdparty:rules_android_ndk.patch",
    ],
)

android_ndk_repository_extension = use_extension("@rules_android_ndk//:extension.bzl", "android_ndk_repository_extension")
android_ndk_repository_extension.configure(
    api_level = 21,
    version = "r29",
)
use_repo(android_ndk_repository_extension, "androidndk")

# Don't register the Android toolchain here, as it will trigger NDK download even when not needed (e.g. on Linux arm64)
# Instead, register it with --extra_toolchains=@androidndk//:all from commandline
# register_toolchains("@androidndk//:all")

bazel_dep(name = "rules_java", version = "9.3.0")
bazel_dep(name = "rules_android", version = "0.7.1")
bazel_dep(name = "protobuf", version = "33.4")  # Note: Needs to be pinned to the same version used by the rules_android. See this comment: https://github.com/bazelbuild/rules_android/issues/435#issuecomment-3753825840

remote_android_extensions = use_extension("@rules_android//bzlmod_extensions:android_extensions.bzl", "remote_android_tools_extensions")
use_repo(remote_android_extensions, "android_tools")

android_sdk_repository_extension = use_extension("@rules_android//rules/android_sdk_repository:rule.bzl", "android_sdk_repository_extension")
use_repo(android_sdk_repository_extension, "androidsdk")

bazel_dep(name = "rules_jvm_external", version = "6.9")

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    name = "android_test_deps",
    artifacts = [
        "junit:junit:4.13.2",
        "androidx.test:rules:1.7.0",
        "androidx.test.ext:junit:1.3.0",
    ],
    repositories = [
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
)
use_repo(maven, "android_test_deps")

# Android rules end

# Emscripten rules begin
bazel_dep(name = "emsdk", version = "4.0.16")
single_version_override(
    module_name = "emsdk",
    patch_strip = 1,
    patches = [
        "//3rdparty:emsdk.patch",
    ],
)

emscripten_cache = use_extension(
    "@emsdk//:emscripten_cache.bzl",
    "emscripten_cache",
)

# NOTE: See tools/emsdk-cache-builder/README.md for instructions on how to build emsdk cache tarball and upload it to the artifactory
# emscripten_cache.prebuilt_cache(
#     http_archive_url = "https://artifactory.yourserver.com:443/artifactory/bazel-prebuilt-packages/emsdk-caches/emsdk-cache-4.0.16.tar.gz",
#     sha256 = "3e88abcbd22bac7b05af416c8f1859d12572c8e9356db604a2768fcfda863da8",
#     strip_prefix = "emsdk-cache",
# )

bazel_dep(name = "posluznik", version = "1.3.5")
git_override(
    module_name = "posluznik",
    commit = "41af6ef76a60d6adb8e8dd0066f2ac8386badc56",
    remote = "https://github.com/microblink/posluznik",
)


# Chrome to run WASM tests on

chrome_extension = use_extension("//macros/wasm-helpers:chrome_repo.bzl", "chrome_extension")
chrome_extension.install_chrome(
    name = "chrome",
    version = "141",
)
use_repo(chrome_extension, "chrome")

# tool for validation of WASM binaries
bazel_dep(name = "wabt", version = "1.0.37")

# Emscripten rules end

# DEV DEPENDENCIES


bazel_dep(name = "googletest", version = "1.17.0.bcr.2", dev_dependency = True)
single_version_override(
    module_name = "googletest",
    patch_strip = 1,
    patches = [
        "//3rdparty:googletest.patch",
    ],
)

bazel_dep(name = "opencv", version = "4.12.0.bcr.1")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
http_archive(
    name = "ubuntu-gtk-deps",
    sha256 = "84656a6df544ecef62169cfe3ab6e41bb4346a62d3ba2a045dc5a0a2ecea94a3",
    urls = ["https://commondatastorage.googleapis.com/chrome-linux-sysroot/toolchain/2202c161310ffde63729f29d27fe7bb24a0bc540/debian_stretch_amd64_sysroot.tar.xz"],
    build_file = "//opencv:ubuntu-gtk-deps.BUILD.bazel",
)
